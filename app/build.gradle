apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply from: '../flavors.gradle'

android {
    compileSdkVersion 29
    buildToolsVersion "29.0.3"

    defaultConfig {
        applicationId "dev.olog.trebuchet"
        minSdkVersion 21
        targetSdkVersion 29
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    flavorDimensions 'scope'
    productFlavors {
        project.flavors.each { flavor, config ->
            "$flavor" {
                dimension 'scope'
                if (flavor != 'full') {
                    versionNameSuffix ".$flavor"
                    if (config.useSeparatePackageName) {
                        // TODO this allows to make a different app for every flavor
                        applicationIdSuffix ".$flavor"
                    }
                }
            }
        }
    }

    project.flavors.each { flavor, config ->
        project.dependencies.add("${flavor}Implementation", project(config.entryModule))
    }

}

dependencies {
    implementation project(':core')
    implementation project(':data')
    implementation project(':navigation')
    // TODO don't need to add dependency for flavor here

    implementation libs.kotlin
    implementation libs.coroutines
    implementation libs.coroutines_android

    implementation libs.x_material

    implementation libs.dagger
    kapt libs.dagger_kapt

    testImplementation 'junit:junit:4.13'
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
}

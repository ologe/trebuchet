// Top-level build file where you can add configuration options common to all sub-projects/modules.

apply from: 'dependencies.gradle'

buildscript {
    ext.kotlin_version = '1.3.72'
    repositories {
        google()
        jcenter()
        maven { url 'https://dl.bintray.com/sargunv/maven' }
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:4.0.0'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "com.google.dagger:hilt-android-gradle-plugin:2.28.1-alpha"

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

allprojects {
    repositories {
        google()
        jcenter()
        maven { url 'https://dl.bintray.com/sargunv/maven' }

    }
    gradle.projectsEvaluated { // TODO remove after coroutines became stable
        tasks.withType(org.jetbrains.kotlin.gradle.tasks.AbstractKotlinCompile).all {
            kotlinOptions.freeCompilerArgs += ["-Xuse-experimental=kotlinx.coroutines.FlowPreview"]
            kotlinOptions.freeCompilerArgs += ["-Xuse-experimental=kotlinx.coroutines.ExperimentalCoroutinesApi"]
            kotlinOptions.jvmTarget = "1.8"
        }
    }

}

subprojects {

    def androidPlugins = [
            "com.android.build.gradle.AppPlugin",
            "com.android.build.gradle.LibraryPlugin"
    ]

    project.plugins.whenPluginAdded { plugin ->
        if (plugin.class.name in androidPlugins) {
            // setup common options
            project.android {
                compileOptions {
                    sourceCompatibility JavaVersion.VERSION_1_8
                    targetCompatibility JavaVersion.VERSION_1_8
                }
            }
        }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}